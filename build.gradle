buildscript {
  repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/plugins-release' }
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: spring_boot_version
    classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: prop_deps_version
    classpath group: 'com.moowork.gradle', name: 'gradle-node-plugin', version: gradle_node_version
  }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
apply plugin: 'com.moowork.node'

war {
  baseName = 'toh'
  version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  providedRuntime
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'javax.inject', name: 'javax.inject', version: javax_inject_version

  optional group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

  providedRuntime group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

task jsBuild(type: NpmTask) {
  args = ['run', 'buildprod']
}

compileJava.dependsOn jsBuild
defaultTasks 'clean', 'bootRun'
